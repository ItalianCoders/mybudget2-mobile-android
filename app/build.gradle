/*
 * Project: mybudget2-mobile-android
 * File: build
 *
 * Created by fattazzo
 * Copyright Â© 2019 Gianluca Fattarsi. All rights reserved.
 *
 * MIT License
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'com.google.gms.google-services'  // Google Play services Gradle plugin
apply plugin: 'io.fabric' // Crashlytics
apply plugin: "kotlin-allopen"

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "it.italiancoders.mybudget"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 62
        versionName "0.9.17"
        testInstrumentationRunner "it.italiancoders.mybudget.runner.CustomTestRunner"

        multiDexEnabled true

        kapt {
            arguments {
                arg("room.schemaLocation", "$projectDir/schemas".toString())
            }
        }
    }
    dataBinding {
        enabled = true
    }
    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        debug {
            debuggable true
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro', 'proguard-test-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
    kotlinOptions {
        jvmTarget = "1.8"
    }
    testOptions {
        unitTests {
            // By default, local unit tests throw an exception any time the code you are testing tries to access
            // Android platform APIs (unless you mock Android dependencies yourself or with a testing
            // framework like Mockito). However, you can enable the following property so that the test
            // returns either null or zero when accessing platform APIs, rather than throwing an exception.
            returnDefaultValues true
        }
        // Disables animations during instrumented tests you run from the cammand line.
        // see [https://google.github.io/android-gradle-dsl/current/com.android.build.gradle.internal.dsl.TestOptions.html#com.android.build.gradle.internal.dsl.TestOptions:animationsDisabled]
        animationsDisabled = true


    }
    sourceSets {
        test.resources.srcDirs += 'src/sharedTest/assets'
        test.java.srcDirs += 'src/sharedTest/java'

        androidTest.java.srcDirs += 'src/sharedTest/java'
        androidTest.resources.srcDirs += 'src/sharedTest/assets'
    }
    buildToolsVersion = '29.0.0'
}

allOpen {
    annotation("it.italiancoders.mybudget.utils.OpenForTesting")
}

dependencies {

    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // AndroidX --------------------------------------
    implementation 'androidx.appcompat:appcompat:1.1.0'
    //implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.recyclerview:recyclerview:1.1.0-beta04'
    implementation "androidx.annotation:annotation:1.1.0"
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-beta2'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0-alpha05'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.2.0-alpha05'

    // Downgrade to 1.0.0 for layout preview errors in design editor ( see toolbar in activity_categories.xml )
    // https://stackoverflow.com/questions/56083927/android-studio-xml-preview-not-showing
    implementation 'com.google.android.material:material:1.0.0'

    // Physics based animation
    implementation 'androidx.dynamicanimation:dynamicanimation-ktx:1.0.0-alpha02'

    // Databinding kotlin
    implementation 'androidx.preference:preference:1.1.0'
    kapt 'com.android.databinding:compiler:3.2.0-alpha10'

    // Firebase SDK
    implementation 'com.google.firebase:firebase-core:17.0.0'
    // Crashlytics SDK
    implementation 'com.crashlytics.sdk.android:crashlytics:2.10.1'

    // Retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.6.1'
    implementation 'com.squareup.retrofit2:converter-gson:2.6.1'
    implementation 'com.jakewharton.retrofit:retrofit2-kotlin-coroutines-adapter:0.9.2'
    implementation 'com.squareup.okhttp3:okhttp:4.2.0'

    // klaxon: json handler
    implementation ('com.beust:klaxon:5.0.1')

    // Material Dialogs
    // https://github.com/afollestad/material-dialogs
    implementation 'com.afollestad.material-dialogs:core:3.1.0'
    implementation 'com.afollestad.material-dialogs:bottomsheets:3.1.0'
    implementation 'com.afollestad.material-dialogs:input:3.1.0'

    // Room
    implementation 'androidx.room:room-runtime:2.2.0-rc01'
    kapt 'androidx.room:room-compiler:2.2.0-rc01'

    // Android-SpinKit
    // https://github.com/ybq/Android-SpinKit
    implementation 'com.github.ybq:Android-SpinKit:1.2.0'

    // MPAndroidChart
    // https://github.com/PhilJay/MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // MonthAndYearPicker
    // https://github.com/premkumarroyal/MonthAndYearPicker
    implementation 'com.whiteelephant:monthandyearpicker:1.3.0'

    // Spotlight
    // https://github.com/TakuSemba/Spotlight
    implementation 'com.github.takusemba:spotlight:1.8.0'

    // Dagger
    implementation 'com.google.dagger:dagger:2.24'
    implementation 'com.google.dagger:dagger-android-support:2.24'
    kapt 'com.google.dagger:dagger-compiler:2.24'
    kapt 'com.google.dagger:dagger-android-processor:2.24'

    // Test ------------------------------------------

    // Local Unti Test
    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13-beta-3'
    // Optional -- Robolectric environment
    testImplementation 'androidx.test:core:1.2.0'
    // Optional -- Mockito framework
    testImplementation 'org.mockito:mockito-core:3.0.0'
    testImplementation 'org.mockito:mockito-inline:2.13.0'

    testImplementation 'org.hamcrest:hamcrest-library:2.1'

    testImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"

    // Test helpers for LiveData
    testImplementation "androidx.arch.core:core-testing:2.1.0"

    // Instrumented Unit Tests
    androidTestImplementation "androidx.test:rules:1.3.0-alpha02"
    androidTestImplementation 'androidx.test:runner:1.3.0-alpha02'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation "androidx.arch.core:core-testing:2.1.0"

    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0-alpha02'
    androidTestImplementation 'androidx.test.espresso:espresso-contrib:3.3.0-alpha02'

    androidTestImplementation 'org.mockito:mockito-android:2.24.5'

    androidTestImplementation 'androidx.test:core:1.2.0'

    androidTestImplementation "com.nhaarman.mockitokotlin2:mockito-kotlin:2.1.0"

    kaptAndroidTest 'com.google.dagger:dagger-compiler:2.24'
    kaptAndroidTest 'com.google.dagger:dagger-android-processor:2.24'

    // Awaitility. Workaround used for wait bottomSheetBehaviour state change instead SystemClock.sleep()
    // https://github.com/awaitility/awaitility
    androidTestImplementation("org.awaitility:awaitility:4.0.1") {
        exclude group: 'org.hamcrest', module: 'hamcrest'
    }
}
